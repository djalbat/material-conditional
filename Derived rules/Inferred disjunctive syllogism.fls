Metavariable P:Statement
Metavariable Q:Statement
Metavariable R:Statement

Rule (InferredDisjunctionElimination)
  Premises
    P ⇒ R
    Q ⇒ R
    P ∨ Q
  Conclusion
    R
  Proof
    Q ⇒ R
    Q → R by LogicalConsequenceInfersMaterialConditional
    ¬Q ∨ R by StrongerMaterialConditionalElimination
    R ∨ ¬Q by DisjunctionCommutes
    P ⇒ R
    P → R by LogicalConsequenceInfersMaterialConditional
    ¬P ∨ R by StrongerMaterialConditionalElimination
    R ∨ ¬P by DisjunctionCommutes
    R ∨ ¬Q
    (R ∨ ¬P) ∧ (R ∨ ¬Q) by ConjunctionIntroduction
    R ∨ (¬P ∧ ¬Q) by DisjunctionDistributedOverConjunction
    P ∨ Q
    ¬(¬P ∧ ¬Q) by DisjunctionInfersNegatedConjunctionOfNegations
    R ∨ (¬P ∧ ¬Q)
    (¬P ∧ ¬Q) ∨ R by DisjunctionCommutes
  Therefore
    R by DisjunctiveSyllogism
